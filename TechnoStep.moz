@OnLoad
  ShowLayout 1
  
  // Knob constants
  knob_direction = 10
  knob_pulse = 9
  knob_offset = 19
  knob_steps = 20
  knob_controlSteps = 21 
  
  // Config knob constants
  knob_scale = 0
  knob_root = 1
  knob_relativePitch = 2
  
  // Visible UI state
  steps = 8
  controlSteps = 8
  pulseDenom = 16
  direction = 1
  offset = 0
  
  // Config state
  relativePitch = False
  rootNote = 0
  
  // Sequencer internal state
  metroPulse = -1
  currentStep = -1
  currentControlStep = -1
  currentStepDelta = 1
  
  lastHostBeat = 0
  lastHostBar = 0
  
  // UI constants
  screen_main = 0
  screen_config = 1
  
  // UI internal state
  doubleShiftValid = False
  activeScreen = screen_main
  knobValues = []
  
  SetKnobValue knob_direction, 127
  SetKnobValue knob_pulse, 100 
  SetKnobValue knob_steps, 127
  SetKnobValue knob_controlSteps, 127  
  SetKnobValue knob_offset, 0  

  SetRootNote 0
  PresetScale 0
  
  for i = 0 to 7
    SetKnobValue i + 11, 100
  endfor
  
  Call @SaveKnobValues
  Call @UpdateUI
  Call @UpdateMetronome
@End 

@OnHostStart
  // Assume we have rewound to the start, ideally this would be more robust
  if HostBar <> lastHostBar OR HostBeat <> lastHostBeat
    metroPulse = -1
  endif
  //Log {Bar }, HostBar, { }, lastHostBar, { Beat }, HostBeat, { }, lastHostBeat
@End 

@OnHostStop
  //currentStep = -1
  //Call @UpdateUI
@End 

@OnKnobChange
  knobValues[LastKnob + (activeScreen * 22)] = GetKnobValue LastKnob
  
  if activeScreen = screen_main
    if LastKnob = knob_direction
      direction = Round(TranslateScale (GetKnobValue knob_direction), 0, 127, 0, 1)
      if direction = 0
        currentStepDelta = -1
      else
        currentStepDelta = 1
      endif
    
      Call @UpdateUI
    elseif LastKnob = knob_offset
      offset = Round(TranslateScale (GetKnobValue knob_offset), 0, 127, 0, 7)

      Call @UpdateUI
    elseif LastKnob = knob_pulse
      pulseDenom = Pow 2, Round(TranslateScale (GetKnobValue knob_pulse), 0, 127, 0, 5) 
      Call @UpdateUI
      Call @UpdateMetronome
    elseif LastKnob = knob_steps
      steps = Round(TranslateScale (GetKnobValue knob_steps), 0, 127, 1, 8)
      Call @UpdateUI
    elseif LastKnob = knob_controlSteps
      controlSteps = Round(TranslateScale (GetKnobValue knob_controlSteps), 0, 127, 1, 8)
      Call @UpdateUI  
    endif
  elseif activeScreen = screen_config
    if LastKnob = knob_scale
      PresetScale Round(TranslateScale (GetKnobValue knob_scale), 0, 127, 0, 24)
      Call @UpdateUI
    elseif LastKnob = knob_root
      rootNote = Round(TranslateScale (GetKnobValue knob_root), 0, 127, 0, 12)
      SetRootNote rootNote
      Call @UpdateUI
    elseif LastKnob = knob_relativePitch
      relativePitch = Round(TranslateScale (GetKnobValue knob_relativePitch), 0, 127, 0, 1)
      Call @UpdateUI
    endif
  endif
@End 

@UpdateUI
  for i = 0 to 21
    LabelKnob i, { }
  endfor
  
  if activeScreen = screen_main
    if direction = 0
      LabelKnob knob_direction, {Dir: Rev}
    elseif direction = 1
      LabelKnob knob_direction, {Dir: Fwd}
    endif
  
    LabelKnob knob_pulse, {Pulse: 1/}, pulseDenom
    LabelKnob knob_steps, {Steps: }, steps
    LabelKnob knob_controlSteps, {C.Steps: }, controlSteps
    LabelKnob knob_offset, {Offset: }, offset

    Call @UpdateStepsUI
  elseif activeScreen = screen_config
    LabelKnob 0, ScaleName
    LabelKnob 1, {Root: }, RootNoteName 
    LabelKnob 2, {Rel: }, relativePitch 
  endif
@End

@UpdateStepsUI
  if activeScreen = screen_main
    for i = 0 to 7
      if i < steps
        if i = currentStep
          LabelKnob i, {*Step }, i + 1, {*}
        else
          LabelKnob i, {Step }, i + 1
        endif
      else 
        LabelKnob i, { } 
      endif
    
      if i < controlSteps
        if i = currentControlStep
          LabelKnob i + 11, {*Ctrl }, i + 1, {*}
        else
          LabelKnob i + 11, {Ctrl }, i + 1
        endif
      else
        LabelKnob i + 11, { }
      endif
    endfor 
  endif
@End

@SwitchScreen
  Call @SaveKnobValues
  activeScreen = nextScreen
  Call @UpdateUI
  Call @RestoreKnobValues
@End

@SaveKnobValues
  for i = 0 to 21
    knobValues[i + (activeScreen * 22)] = GetKnobValue i
  endfor
@End

@RestoreKnobValues
  for i = 0 to 21
    SetKnobValue i, knobValues[i + (activeScreen * 22)]
  endfor
@End

@UpdateMetronome
  SetMetroPPQN pulseDenom / 4
@End

@OnMetroPulse
  metroPulse = metroPulse + 1
  currentStep = (metroPulse + offset) % steps
  currentControlStep = ((metroPulse + offset) % controlSteps)

  if relativePitch
    note = rootNote + knobValues[currentStep]
  else
    note = knobValues[currentStep]
  endif
  
  note = ScaleQuantize (note)
  control = knobValues[currentControlStep + 11]
  
  if note > 0
    SendMIDINoteOn 0, note, control, 0
    SendMIDINoteOff 0, note, control, 50
  endif  

  Call @UpdateStepsUI
@End

@OnNewBar
  lastHostBar = HostBar 
@End

@OnNewBeat
  lastHostBeat = HostBeat
@End

@OnShiftUp
  if NOT doubleShiftValid
    doubleShiftValid = True
    SetTimerInterval 500
    StartTimer
  else 
    if activeScreen = screen_main
      nextScreen = screen_config
      Call @SwitchScreen
    elseif activeScreen = screen_config
      nextScreen = screen_main
      Call @SwitchScreen
    endif
    
    Call @UpdateUI
    doubleShiftValid = False
  endif
@End 

@OnTimer
  doubleShiftValid = False
  StopTimer
@End